//키패드 스마트 도어락

#include <Keypad.h>
#include <Servo.h>

// ===== 핀 설정 =====
const int SERVO_PIN    = 10;
const int BUZZER_PIN   = 11;
const int YELLOW_LED   = 12;  // 성공(노란)
const int RED_LED      = 13;  // 실패(빨간)

// ===== 키패드 설정 =====
const byte ROWS = 4;
const byte COLS = 4;

char keys[ROWS][COLS] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};

// 키패드 리본 1~8을 D9~D2에 연결했다고 가정
// 1->D9, 2->D8, 3->D7, 4->D6, 5->D5, 6->D4, 7->D3, 8->D2
byte rowPins[ROWS] = {9, 8, 7, 6};
byte colPins[COLS] = {5, 4, 3, 2};

Keypad keypad = Keypad(makeKeymap(keys), rowPins, colPins, ROWS, COLS);
Servo lockServo;

// ===== 비밀번호 =====
String password = "1234";   // 비번 1234로.
String input = "";

// 서보 각도
const int OPEN_ANGLE  = 90;
const int CLOSE_ANGLE = 0;

// ===== 함수 =====
void beepTone(int ms, int freq = 2000) {
  tone(BUZZER_PIN, freq);
  delay(ms);
  noTone(BUZZER_PIN);
}

void blinkPin(int pin, int times, int onMs, int offMs) {
  for (int i = 0; i < times; i++) {
    digitalWrite(pin, HIGH);
    delay(onMs);
    digitalWrite(pin, LOW);
    delay(offMs);
  }
}

void allLedsOff() {
  digitalWrite(YELLOW_LED, LOW);
  digitalWrite(RED_LED, LOW);
}

void successAction() {
  allLedsOff();

  //성공: 노란 LED 켜지기 + 소리
  blinkPin(YELLOW_LED, 6, 120, 120);
  beepTone(120, 2500);

  // 문 열기
  lockServo.write(OPEN_ANGLE);
  delay(3000);

  // 문 닫기
  lockServo.write(CLOSE_ANGLE);

  // 한 번 더 불켜지기
  blinkPin(YELLOW_LED, 2, 150, 150);
}

void wrongAction() {
  allLedsOff();

  // 실패: 빨간 LED 켜짐 + 경고음
  digitalWrite(RED_LED, HIGH);
  beepTone(150, 1600);
  delay(80);
  beepTone(150, 1600);

  delay(800);
  digitalWrite(RED_LED, LOW);
}

void setup() {
  pinMode(YELLOW_LED, OUTPUT);
  pinMode(RED_LED, OUTPUT);
  allLedsOff();

  lockServo.attach(SERVO_PIN);
  lockServo.write(CLOSE_ANGLE);

  Serial.begin(9600);
  Serial.println("READY: enter password then press #, * to clear");
  Serial.print("Password = ");
  Serial.println(password);
}

void loop() {
  char key = keypad.getKey();
  if (!key) return;

  // 키 입력 피드백
  beepTone(20, 2400);

  // 입력 초기화
  if (key == '*') {
    input = "";
    allLedsOff();
    return;
  }

  // 확인
  if (key == '#') {
    if (input == password) {
      successAction();
    } else {
      wrongAction();
    }
    input = "";
    return;
  }

  // 일반 키 누적
  if (input.length() < 8) {
    input += key;
  }
}
