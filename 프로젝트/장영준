#include <Wire.h>
#include <LiquidCrystal_I2C.h>

#define TRIG_PIN 8
#define ECHO_PIN 9

#define BUZZER_PIN 13

#define RED_PIN   10
#define GREEN_PIN 11
#define BLUE_PIN  12

#define POT_PIN A0

LiquidCrystal_I2C lcd(0x27, 16, 2);

long getDistance() {
  digitalWrite(TRIG_PIN, LOW);
  delayMicroseconds(2);

  digitalWrite(TRIG_PIN, HIGH);
  delayMicroseconds(10);
  digitalWrite(TRIG_PIN, LOW);

  long duration = pulseIn(ECHO_PIN, HIGH, 30000);
  if (duration == 0) return -1;

  return duration * 0.034 / 2;
}

void setColor(bool r, bool g, bool b) {
  digitalWrite(RED_PIN, r);
  digitalWrite(GREEN_PIN, g);
  digitalWrite(BLUE_PIN, b);
}

void beep(int onTime, int offTime) {
  digitalWrite(BUZZER_PIN, HIGH);
  delay(onTime);
  digitalWrite(BUZZER_PIN, LOW);
  delay(offTime);
}

void setup() {
  pinMode(TRIG_PIN, OUTPUT);
  pinMode(ECHO_PIN, INPUT);

  pinMode(BUZZER_PIN, OUTPUT);

  pinMode(RED_PIN, OUTPUT);
  pinMode(GREEN_PIN, OUTPUT);
  pinMode(BLUE_PIN, OUTPUT);

  lcd.init();
  lcd.backlight();

  lcd.setCursor(0, 0);
  lcd.print("Parking Assist");
  lcd.setCursor(0, 1);
  lcd.print("System Ready");
  delay(1500);
  lcd.clear();
}

void loop() {
  long distance = getDistance();

  int potValue = analogRead(POT_PIN);
  int warnDist = map(potValue, 0, 1023, 15, 40);

  lcd.setCursor(0, 0);
  lcd.print("Dist: ");
  if (distance < 0) {
    lcd.print("ERR   ");
  } else {
    lcd.print(distance);
    lcd.print("cm   ");
  }

  lcd.setCursor(0, 1);
  lcd.print("Warn< ");
  lcd.print(warnDist);
  lcd.print("cm   ");

  if (distance < 0 || distance > warnDist) {
    setColor(0, 1, 0);   // 초록
    digitalWrite(BUZZER_PIN, LOW);
  }
  else if (distance > warnDist /2) {
    setColor(1, 1, 0);   // 노랑

    int interval = map(distance, warnDist, warnDist / 2, 400, 150);
    interval = constrain(interval, 150, 400);

    beep(80, interval);
  }
  else {
    setColor(1, 0, 0);   // 빨강

    int interval = map(distance, warnDist / 2, 5, 150, 50);
    interval = constrain(interval, 50, 150);

    beep(80, interval);
  }


  delay(150);
}
